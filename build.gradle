buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:2.0.2"
    }
}

plugins {
    id 'idea'
    id 'org.jetbrains.kotlin.jvm' version '1.4.10'
    id 'com.github.johnrengelman.shadow' version '2.0.4'
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group "net.trivernis"
version PLUGIN_VERSION
sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://hub.spigotmc.org/nexus/content/repositories/snapshots"
    }
    maven {
        url 'https://mvnrepository.com/artifact/org.xerial/sqlite-jdbc'
    }
    maven {
        name 'papermc'
        url 'https://papermc.io/repo/repository/maven-public/'
    }
    maven {
        name 'CodeMc'
        url 'https://repo.codemc.org/repository/maven-public'
    }

    maven {
        name 'mikeprimm'
        url 'https://repo.mikeprimm.com'
    }

    maven {
        url 'https://jitpack.io'
    }
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "com.destroystokyo.paper:paper-api:1.14.4-R0.1-SNAPSHOT"
    compileOnly "org.dynmap:dynmap-api:2.0"
    compileOnly group: 'org.xerial', name: 'sqlite-jdbc', version: '3.28.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "io.papermc:paperlib:1.0.6"
    implementation "org.bstats:bstats-bukkit:1.5"

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation 'io.kotest:kotest-runner-junit5:4.3.2'
    testImplementation 'io.mockk:mockk:1.10.6'
    testImplementation "org.dynmap:dynmap-api:2.0"
}

apply plugin: "com.github.johnrengelman.shadow"
apply plugin: 'java'

shadowJar {
    relocate 'io.papermc.lib', 'net.trivernis.chunkmaster.paperlib'
    relocate 'org.bstats', 'net.trivernis.chunkmaster.bstats'
    relocate 'kotlin', 'net.trivernis.chunkmaster.kotlin'
    relocate 'org.intellij', 'net.trivernis.chunkmaster.intellij'
    relocate 'org.jetbrains', 'net.trivernis.chunkmaster.jetbrains'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    with copySpec {
        from 'src/main/resources/'
        include 'plugin.yml'
        filter { String line ->
            line.replace('$$PLUGIN_VERSION$$', PLUGIN_VERSION)
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}